#compdef fast-theme
#----------------------------------------------------------------------------------------------------------------------
# -*- mode: zsh; sh-indentation: 2; indent-tabs-mode: nil; sh-basic-offset: 2; -*-
# vim: ft=zsh sw=2 ts=2 et
#----------------------------------------------------------------------------------------------------------------------
#
# Copyright (c) 2018 Sebastian Gniazdowski
# Copyright (c) 2022-present, Z-Shell Community
#
# Completion for theme-switching function, fast-theme,
# part of z-shell/F-Sy-H, zdharma/fast-syntax-highlighting.
#
builtin emulate -L zsh ${=${options[xtrace]:#off}:+-o xtrace}
builtin setopt extended_glob local_options warn_create_global typeset_silent no_short_loops rc_quotes no_auto_pushd

integer ret=1
typeset -a themes themes2 themes3 arguments expl
themes=( "$FAST_WORK_DIR"/themes/*.ini(:t:r) )

arguments=(
  {-w,--workdir}'[cd into $FAST_WORK_DIR (if not set, then into the plugin directory)]'
  {-R,--ov-reset}'[unset overlay, use styles only from main-theme (requires restart)]'
  {-p,--palette}'[just print all 256 colors and exit (useful when creating a theme)]'
  {-r,--reset}'[unset any theme (revert to default highlighting)]'
  {-t,--test}'[test theme after setting it (show example code)]'
  {-s,--show}'[get and display the theme currently being set]'
  {-v,--verbose}'[more messages during operation]'
  {-i,--info}'[additional information]'
  {-l,--list}'[list available themes]'
  {-q,--quiet}'[no default messages]'
  {-h,--help}'[display help text]'
)

if [[ -d ${XDG_CONFIG_HOME:-$HOME/.config}/fsh ]]; then
  themes2=( "${XDG_CONFIG_HOME:-$HOME/.config}"/fsh/*.ini(:t:r) )
  themes+=( CONFIG:${^themes2[@]} )
elif [[ -d ${XDG_CACHE_HOME:-$HOME/.cache}/zi/fsh ]]; then
  themes3=( "${XDG_CACHE_HOME:-$HOME/.cache}"/zi/fsh/*.ini(:t:r) )
  themes+=( CACHE:${^themes3[@]} )
fi

_wanted themes expl "Themes" compadd "$@" -a - themes && ret=0
_arguments -s $arguments && ret=0

return $ret
